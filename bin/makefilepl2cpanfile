#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Long;
use File::Slurp qw(read_file);
use Text::Diff;
use App::makefilepl2cpanfile;

my ($with_dev, $no_dev, $dry, $diff);

GetOptions(
	'with-develop' => \$with_dev,
	'no-develop'   => \$no_dev,
	'dry-run'	  => \$dry,
	'diff'		 => \$diff,
);

$with_dev //= !$no_dev;

my $existing = -r 'cpanfile' ? read_file('cpanfile') : '';

my $out = App::makefilepl2cpanfile::generate(
	makefile	  => 'Makefile.PL',
	existing	  => $existing,
	with_develop  => $with_dev,
);

if ($diff && $existing) {
	print diff \$existing, \$out, { STYLE => 'Unified' };
	exit;
}

if ($dry) {
	print $out;
	exit;
}

open my $fh, '>', 'cpanfile' or die $!;
print $fh $out;
close $fh;

print "cpanfile written\n";
